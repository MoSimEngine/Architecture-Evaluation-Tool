plugins {
	id 'java-library'
	id 'maven-publish'
	id 'java-library-distribution'
	id "com.github.johnrengelman.shadow" version "7.1.0"
	id "net.ltgt.errorprone" version "2.0.2"
	id "com.diffplug.spotless" version "5.16.0"
}

repositories {
	mavenLocal()
	mavenCentral()
	gradlePluginPortal()
}

dependencies {
	implementation 'fr.inria.gforge.spoon:spoon-core:9.2.0-beta-5'
	implementation 'com.google.flogger:flogger:0.6'
	implementation 'com.google.flogger:flogger-system-backend:0.6'
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.2'
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.2'
	testImplementation "com.google.truth:truth:1.1.3"
	implementation 'com.google.guava:guava:30.1-jre'
	errorprone("com.google.errorprone:error_prone_core:2.9.0")
	testImplementation "com.google.truth:truth:1.1.2"
}

group = 'edu.kit.kastel.sdq.case4lang.refactorlizar'
version = '0.0.1'

distributions {
	main {
		distributionBaseName = 'architecture-evaluation-tool'
		contents {
			duplicatesStrategy = DuplicatesStrategy.EXCLUDE
		}
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			from(components.java)
		}
	}
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}
java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(11)
	}
}
test {
	useJUnitPlatform()
}
description = 'architecture-evaluation'

spotless {
	java {
		target "**/*.java"
		targetExclude "**/test/resources/**", "**/build/**"
		trimTrailingWhitespace()
		removeUnusedImports()
		googleJavaFormat().aosp()
	}
	groovy {
		target "**/*.gradle"
		// Use the default version and Groovy-Eclipse default configuration
		greclipse()
	}
}